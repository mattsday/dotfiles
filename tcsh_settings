# Matt Day's custom .tcsh file
# These settings apply only to tcsh and not to regular old csh

# Load any local config first (aliases should go in _local file)
if ( -e "$HOME/.tcshrc_config" ) then
	source "$HOME/.tcshrc_config"
endif


history -M
set histfile = ~/.tcsh_history
set autolist
set colorcat

# Test for number of colours (and provide good example why c shell sucks!)
set colours = `sh -c 'tput colors 2>/dev/null'`

if ( $? > 0 ) then
	# If it's reporting as a 256-colour term try changing to just an xterm
	if ( "$TERM" == "xterm-256color") then
		setenv TERM xterm
	endif
	set colours = `sh -c 'tput colors 2>/dev/null'`
	# If that still fails then disable colour
	if ( $? > 0 ) then
		set colours = 7
	endif
endif

# By default disable colour for root (enable by creating ~/.enable_shell_colour):
if ( `id -u` == 0 ) then
	set colours = 7
endif

# Check if colour is disabled manually
if ( -e ~/.enable_shell_colour ) then
	set colours = 256
else if ( -e ~/.disable_shell_colour ) then
	set colours = 7
endif

if ( $colours >= 8 ) then
	# Load colour aliases
	set yellow = "%{\033[1;33m%}"
	set  green = "%{\033[1;32m%}"
	set   cyan = "%{\033[1;36m%}"
	set   grey = "%{\033[1;30m%}"
	set    end = "%{\033[0m%}" 
	# If the user is 'matt' don't print it
	if ( $USER == "matt" || $USER == "mattsday" ) then
		set prompt = "${yellow}%m${end}:${cyan}%~${end}%# "
	else
		set prompt = "${green}%n${end}@${yellow}%m${end}:${cyan}%~${end}%# "
	endif
	# For some reason the colour isn't resetting unless I put a space here (iTerm bug?)
	# set rprompt = "${end}[%?]${grey}[%P]${end} "
	# Removed colour for now:
	set rprompt = "[%?][%P]"

	# Cleanup
	unset yellow cyan end grey green
else
	if ( $USER == "matt" || $USER == "mattsday" ) then
		set prompt = "%m:%~%# "
	else
		set prompt = "%n@%m:%~%# "
	endif
	set rprompt = "[%?][%P]"
endif

# Use > and # prompts for users and root respectively
set promptchars = '>#'

# Use modern echo style
set echo_style = 'both'

# Emacs mode
bindkey -e
bindkey "^W" backward-delete-word
bindkey -k up history-search-backward
bindkey -k down history-search-forward
bindkey "^[[3~" delete-char

# vim: syntax=tcsh
